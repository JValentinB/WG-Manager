// Based on Exercise 5
module UI

import src/utils/dom
// import src/utils/misc

import ref
import string


record Application[Ev](
  update: Ev => Unit at {io, global, async},
  view: () => HtmlElement[Ev] at {io, global, async}
)

record Action(
  f: () => Unit at {io, global, async}
)

record HtmlElement[Ev] (
  node: Node,
  tag: String,
  text: String, 
  app: Option[Application[Ev]],
  children: List[HtmlElement[Ev]], 
  attributes: List[Attribute],
  handlers: List[EventHandler[Ev]]
)

record Attribute (
  name: String,
  value: String
)

type EventHandler[Ev] { 
  OnClick(handler: Ev);
  OnChange(handler: Ev);
  
  OnMouseEnter(handler: Ev);
  OnMouseLeave(handler: Ev)
}

// effect addingChild[Ev](child: HtmlElement[Ev]): Unit

def button[Ev](text: String, children: List[HtmlElement[Ev]], attributes: List[Attribute], handlers: List[EventHandler[Ev]], app: Option[Application[Ev]]) =
  HtmlElement(createElement("button"), "button", text, app, children, attributes, handlers)

def div[Ev](text: String, children: List[HtmlElement[Ev]], attributes: List[Attribute], handlers: List[EventHandler[Ev]], app: Option[Application[Ev]]) =
  HtmlElement(createElement("div"), "div", text, app, children, attributes, handlers)

def input[Ev](text: String, children: List[HtmlElement[Ev]], attributes: List[Attribute], handlers: List[EventHandler[Ev]], app: Option[Application[Ev]]) =
  HtmlElement(createElement("input"), "input", text, app, children, attributes, handlers)
def textarea[Ev](text: String, children: List[HtmlElement[Ev]], attributes: List[Attribute], handlers: List[EventHandler[Ev]], app: Option[Application[Ev]]) =
  HtmlElement(createElement("textarea"), "textarea", text, app, children, attributes, handlers)



def appendChild[Ev](parent: HtmlElement[Ev], child: HtmlElement[Ev]): HtmlElement[Ev] = {
  val newChildren = parent.children.append([child])
  HtmlElement(parent.node, parent.tag, parent.text, parent.app, newChildren, parent.attributes, parent.handlers)
}
def appendChildren[Ev](parent: HtmlElement[Ev], children: List[HtmlElement[Ev]]): HtmlElement[Ev] = {
  val newChildren = parent.children.append(children)
  HtmlElement(parent.node, parent.tag, parent.text, parent.app, newChildren, parent.attributes, parent.handlers)
}

def addHandlers[Ev](element: HtmlElement[Ev], handlers: List[EventHandler[Ev]]): HtmlElement[Ev] = {
  val newHandlers = element.handlers.append(handlers)
  val newElement = HtmlElement(element.node, element.tag, element.text, element.app, element.children, element.attributes, newHandlers)
  newElement
}



interface ServerMessage {
  def success(): Unit
  def error(msg: String): Unit
}
def checkResponse(response: String): Unit / ServerMessage = {
  with on[OutOfBounds].default { consoleLog("Index out of bounds") };
  with on[WrongFormat].default { consoleLog("Wrong Format") };

  var length = response.length
  var res = response.substring(1, length - 1)
  val list = res.split(":")

  val key = list.get(0)
  val value = list.get(1)
  if (key == "\"success\"" && value == "true") {
    do success()
  } else if (key == "\"error\"") {
    do error(value)
  } else {
    do error("Something went wrong")
  }
}
def handleServerResponse(response: String) {f: () => Unit}: Unit = {
  try {
    checkResponse(response)
  } with ServerMessage {
    def success() = {
      f()
      resume(())
    }
    def error(msg: String) = {
      consoleError(msg)
      alert(msg)
      resume(())
    }
  }
}



def toggleDisplay(node: Node): Unit = {
  val currentDisplay = node.getStyleValue("display")
  if (currentDisplay == "none") {
    node.setStyle("display", "block")
    ()
  } else {
    node.setStyle("display", "none")
    ()
  }
}
def toggleDisplay(node: Node, show: Bool): Unit = {
  if (show) {
    node.setStyle("display", "block")
    ()
  } else {
    node.setStyle("display", "none")
    ()
  }
}
def toggleDisplay(node: Node, show: Bool, mode: String): Unit = {
  if (show) {
    node.setStyle("display", mode)
    ()
  } else {
    node.setStyle("display", "none")
    ()
  }
}
